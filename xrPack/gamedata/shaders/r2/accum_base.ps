#include "common.h"
#include "lmodel.h"
#include "shadow.h"

#include "stalker_Shadows.h"

float3 FetchNormal( const float2 uv)
{

	const float3 c = tex2D(s_position, uv).xyz;
	const float3 r = tex2D(s_position, uv + half2( +1.0 , 0.0 ) * STALKER_SCREEN_SIZE.zw ).xyz;
	const float3 l = tex2D(s_position, uv + half2( -1.0 , 0.0 ) * STALKER_SCREEN_SIZE.zw ).xyz;
	const float3 t = tex2D(s_position, uv + half2(  0.0, +1.0 ) * STALKER_SCREEN_SIZE.zw ).xyz;
	const float3 b = tex2D(s_position, uv + half2(  0.0, -1.0 ) * STALKER_SCREEN_SIZE.zw ).xyz;
	const float3 vr = ( r - c ), vl = ( c - l ), vt = ( t - c ), vb = ( c - b );
	const float3 min_horiz = ( dot( vr, vr ) < dot( vl, vl ) ) ? vr : vl;
	const float3 min_vert = ( dot( vt, vt ) < dot( vb, vb ) ) ? vt : vb;
	const float3 normalScreenSpace = normalize( cross( min_horiz, min_vert ) );

	return float3( normalScreenSpace.x, normalScreenSpace.y, normalScreenSpace.z );
}

float RayAttenBorder (float2 pos, float value)
{
	float borderDist = min(1.0 - max(pos.x, pos.y), min(pos.x, pos.y));
	return saturate(borderDist > value ? 1.0 : borderDist / value);
}

inline bool is_saturated(float value)   { return value == saturate(value); }
inline bool is_saturated(float2 value)  { return is_saturated(value.x) && is_saturated(value.y); }
inline bool is_saturated(float3 value)  { return is_saturated(value.x) && is_saturated(value.y) && is_saturated(value.z); }
inline bool is_saturated(float4 value)  { return is_saturated(value.x) && is_saturated(value.y) && is_saturated(value.z) && is_saturated(value.w); }

//////////////////////////////////////////////////////////////////////////////////////////
// This is the basic primitive used by convex, volumetric lights
// for example spot-lights, one face of the omni lights, etc.
//////////////////////////////////////////////////////////////////////////////////////////
// following options are available to configure compilation:
// USE_LMAP
// USE_LMAPXFORM
// USE_SHADOW
//////////////////////////////////////////////////////////////////////////////////////////

uniform float4 m_lmap[2];

#ifdef STALKER_CHECKERBOARD_RENDERING
	float4 	main(AccumBaseVertexOuput Input) : COLOR
#else
	void main 
	( 
		AccumBaseVertexOuput Input,
		out float4 outDiffuse : COLOR0,
		out float4 outSpecular : COLOR1
	)
#endif
{
	// COORD
	float2 uv = Input.tc.xy / Input.tc.w;
	int2 pos = uv * STALKER_SCREEN_SIZE.xy;
	//

	// G-BUFFER
	float4 gBuffer0 = tex2D(s_position, uv);
		if(gBuffer0.z <= 0 ) gBuffer0.z = dx_Viewport_Params.y;
		if(gBuffer0.w <= 0 ) gBuffer0.w = 1.0f;
	float4 gBuffer1 = tex2D(s_normal, uv);
	float4 gBuffer2 = tex2D(s_diffuse, uv);
	float4 gBuffer3 = tex2D(s_specular, uv);
	//
	
	GBufferData data = DataFromGBuffer(gBuffer0, gBuffer1, gBuffer2, gBuffer3);

	// DATA
	//float4 positionView	= tex2Dproj(s_position, Input.tc);
	//float4  normalView = tex2Dproj (s_normal, Input.tc);

	// DATA
	float ambientOcclusionBaked = data.occlusion;
				
	float materialID = DecodeShadingModel(data.materialID);
	
	float depth = data.depth;
	
	float linearDepth = gBuffer0.z;
		
	float3 positionView = float3(linearDepth * (pos * dx_Pos_Decompression_Params.zw - dx_Pos_Decompression_Params.xy), linearDepth);

	float3 normalView = normalize(data.normal);
	
	float3 albedo = data.diffuseColor;
	float3 specularColor = data.specularColor;
	
	float smoothness = data.smoothness;
	//

	// RANDOM
	float dither =	 Dither8x8Bayer
	(
		fmod(pos.x, 8.0f),
		fmod(pos.y, 8.0f)
	);
	
	float4 blueNoise = tex2D(s_bluenoise, uv * (STALKER_SCREEN_SIZE.xy) / 1024);
	
	#ifdef STALKER_USE_TAA
		blueNoise.x = fmod(blueNoise.x + (GOLDEN_RATIO) * (timers.x % 100), 1);
		blueNoise.y = fmod(blueNoise.y + (GOLDEN_RATIO + GOLDEN_RATIO) * (timers.x % 100), 1);
		blueNoise.z = fmod(blueNoise.z + (GOLDEN_RATIO + GOLDEN_RATIO + GOLDEN_RATIO) * (timers.x % 100), 1);
		blueNoise.w = fmod(blueNoise.w + (GOLDEN_RATIO + GOLDEN_RATIO + GOLDEN_RATIO + GOLDEN_RATIO) * (timers.x % 100), 1);
	#endif
		
	float interleavedGradientNoise = InterleavedGradientNoise(pos);
	
	#ifdef STALKER_USE_TAA
		interleavedGradientNoise = fmod(interleavedGradientNoise + (GOLDEN_RATIO) * (timers.x % 100), 1);
	#endif
	
	float2 rotation; 
	sincos(blueNoise.x * STALKER_TWO_PI, rotation.x, rotation.y);
	//

	// BRDF
	float rsqr;
	float3	diffuseDirect, specularDirect;
	plight_local(diffuseDirect, specularDirect, specularColor, smoothness, positionView, normalView, Ldynamic_pos.xyz, Ldynamic_pos.w, rsqr, materialID);

	// SHADOWS
	float4 P4 = float4(positionView.xyz, 1.0f);
	float4 PS = mul(m_shadow, P4);
	float shadows = 1.0f;
	
	#ifdef USE_SHADOW
		shadows = sunmask(P4);
		
		#ifdef USE_SJITTER
		  shadows *= shadow_jitter(PS, uv * STALKER_SCREEN_SIZE / 1024);
		#else
		  shadows *= shadow(PS);
		#endif
	#endif
	
    // SSRS
    #if defined (USE_SHADOW) && defined (STALKER_USE_SSRS_POINT_SPOT)
		float rayLength = STALKER_SSRS_SUN_RAY_LENGTH;
		
		const uint NUM_RAY_MARCH_SAMPLES = 16;
		
		const float SIZE_RAY_MARCH_STEPS = (rayLength / ((float)NUM_RAY_MARCH_SAMPLES));
		
		float reflectionRayMarchStep = SIZE_RAY_MARCH_STEPS;
		reflectionRayMarchStep = reflectionRayMarchStep * blueNoise.x + reflectionRayMarchStep;
		
		float3 L2P = positionView - Ldynamic_pos.xyz;
	    float3 L = normalize(L2P);
		
		float2 rayUV = 0.0f;
		float3 rayPosition = positionView;	
		float3 rayDirection = -L;
		
		uint hitFail = 0; 
		
		[unroll (NUM_RAY_MARCH_SAMPLES)]
		for (uint rayStepIdx = 0; rayStepIdx < NUM_RAY_MARCH_SAMPLES; rayStepIdx++)
		{
			rayPosition += rayDirection * reflectionRayMarchStep;
			
			float4 screenSpaceReflectionPoint = mul(dx_matrix_Projection, float4(rayPosition, 1.0f));
			screenSpaceReflectionPoint /= screenSpaceReflectionPoint.w;
			screenSpaceReflectionPoint.xy = screenSpaceReflectionPoint.xy * float2(0.5, -0.5) + float2(0.5, 0.5);
			
			rayUV = screenSpaceReflectionPoint.xy;
			
			if (is_saturated(rayUV))
			{
				float ZBufferVal = tex2Dlod(s_position, float4(rayUV.xy,0,0)).z;
					if(ZBufferVal <= 0 ) ZBufferVal = dx_Viewport_Params.y;
					
				float delta = rayPosition.z - ZBufferVal;

				float thickness = 0.05f;

				if (abs(thickness - delta) < thickness)
				{
					hitFail = 1;
					break;
				}
			}
			//reflectionRayMarchStep *= 1.0f + SIZE_RAY_MARCH_STEPS; // Increase step size after each iteration
		}

		shadows *= lerp(1.0f, 1.0f - (float)hitFail, RayAttenBorder(rayUV, 0.0125f));
    #endif

	// LIGHTMAP
	float4 lightmap = 1.0f;
	#ifdef USE_LMAP
		#ifdef USE_LMAPXFORM
			PS.x = dot(P4, m_lmap[0]);
			PS.y = dot(P4, m_lmap[1]);
		#endif
		lightmap = tex2Dproj(s_lmap, PS);
	#endif
	
	float3 radiance = Ldynamic_color.rgb;
	
	outDiffuse = float4(diffuseDirect * radiance * shadows * lightmap.rgb, 0.0f);
	outSpecular = float4(specularDirect * radiance * shadows * lightmap.rgb, 0.0f);
}
